buildscript {
    dependencies {
        classpath group: 'org.sonarsource.scanner.gradle', name: 'sonarqube-gradle-plugin', version: '3.3'
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.4.34")
        classpath("net.serenity-bdd:serenity-single-page-report:2.4.48")
    }
}

plugins {
    id 'application'
    id 'pmd'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.gorylenko.gradle-git-properties' version '2.3.1'
    id 'info.solidsoft.pitest' version '1.5.2'
    id 'au.com.dius.pact' version '4.2.7'
    id 'uk.gov.hmcts.java' version '0.12.8'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: 'project-report'


gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/src/main/resources/uk/gov/hmcts/probate/services/orchestrator")
    keys = ['git.commit.id', 'git.commit.time']
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "GMT"
}

group = 'uk.gov.hmcts.probate'
version = "4.0.0"
mainClassName = 'uk.gov.hmcts.probate.OrchestratorApplication'

sourceCompatibility = 11
targetCompatibility = 11

//check.dependsOn dependencyCheckAnalyze
// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

project.ext {
    pactVersion = getCheckedOutGitCommitHash()
}
jacoco {
    toolVersion = '0.8.5'
}

sonarqube {
    properties {
        property "sonar.exclusions", "**uk/gov/hmcts/probate/appinsights/*, **/PaMapper.java, **/IntestacyMapper.java, " +
                "**/ScheduledConfiguration.java, **/AuthenticateUserResponse.java, **/TokenExchangeResponse.java, " +
                "**/FormDataMigrator.java, **/IdamUserEmail.java, **/IdamUsersCsvLoader.java"
        property "sonar.projectName", "PROBATE :: Probate-Orchestrator-Service"
        property "sonar.projectKey", "Probate-Orchestrator-Service"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.host.url", "https://sonar.reform.hmcts.net/"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

test.finalizedBy jacocoTestReport

dependencyCheck {
    suppressionFile = 'dependency-check-suppressions.xml'
}

pitest {
    targetClasses = ['uk.gov.hmcts.probate.*']
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 50
    timeoutConstInMillis = 30000
}

sonarqube {
    properties {
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

sourceSets {
    testSmoke {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }
    testFunctional {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }
    testContract {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }

}

task smoke(type: Test) {
    description = "Runs Smoke Tests"
    testClassesDirs = sourceSets.testSmoke.output.classesDirs
    classpath = sourceSets.testSmoke.runtimeClasspath
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

task functional(type: Test) {
    description = "Runs functional Tests"
    testClassesDirs = sourceSets.testFunctional.output.classesDirs
    classpath = sourceSets.testFunctional.runtimeClasspath
    finalizedBy aggregate
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

functional.finalizedBy(aggregate)

serenity {
    reports = ["single-page-html"]
}

task runProviderPactTests(type: Test) {
    description = "Runs Contract Tests"
    testClassesDirs = sourceSets.testContract.output.classesDirs
    classpath = sourceSets.testContract.runtimeClasspath
}

task runConsumerPactTests(type: Test) {
    description = "Runs Contract Tests"
    useJUnitPlatform()
    testClassesDirs = sourceSets.testContract.output.classesDirs
    classpath = sourceSets.testContract.runtimeClasspath
}

def versions = [
        commonsIO                       : '2.8.0',
        hmctsJavaLogging                : '5.1.5',
        restAssured                     : '3.0.3',
        serviceTokenGenerator           : '0.6.0',
        lombok                          : '1.18.8',
        springfoxSwagger                : '2.10.5',
        springCloudWiremock             : '2.1.5.RELEASE',
        springBootVersion               : '2.3.4.RELEASE',
        springSecurityVersion           : '5.3.5.RELEASE',
        mapStruct                       : '1.3.0.Final',
        authCheckerLib                  : '2.1.4',
        springCloudStarterNetflixHystrix: '2.1.3.RELEASE',
        serenity                        : '2.0.23',
        serenityreporter                : '2.1.4',
        serentity                       : '2.0.17',
        jjwt                            : '0.9.0',
        serviceAuthProviderClient       : '3.1.1',
        restAssured                     : '3.0.7',
        piTest                          : '1.3.2',
        commonMark                      : '0.11.0',
        probateCommonsVersion           : '1.2.3',
        jacksonDatabind                 : '2.9.8',
        pact_version                    : '3.5.24',
        feignHttpClient                 : '10.2.3'

]

jacocoTestReport {
    executionData(test)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}


pmd {
    ignoreFailures = true
    reportsDir = file("$project.buildDir/reports/pmd")
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "https://jitpack.io" }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springcloud_version}"
    }
    dependencies {
        //CVE-2020-26939
        dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.68'

        //CWE79
        dependency group: 'com.netflix.hystrix', name: 'hystrix-core', version: '1.5.18'

        //CVE-2020-15250
        dependency group: 'junit', name: 'junit', version: '4.13.2'

        //CVE-2021-22112
        dependencySet(group: 'org.springframework.security', version: '5.4.6') {
            entry 'spring-security-config'
            entry 'spring-security-core'
            entry 'spring-security-crypto'
            entry 'spring-security-web'
        }

        //CVE-2021-22118
        dependencySet(group: 'org.springframework', version: '5.2.9.RELEASE') {
            entry 'spring-aop'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-context-support'
            entry 'spring-core'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-web'
            entry 'spring-webmvc'
        }
    }
}
processResources {
    from('src/lib/AI-Agent.xml') {
        into 'lib'
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group in ['org.apache.tomcat.embed']) {
                details.useVersion '9.0.43'
            }
            if (details.requested.group == 'org.gradle') {
                details.useVersion '1.4'
                details.because 'API breakage in higher versions'
            }
        }
    }
}

dependencies {
    implementation('com.fasterxml.jackson.core:jackson-databind:2.11.2') {
        force = true
    }
    implementation('io.github.openfeign:feign-core:10.2.3') {
        force = true
    }

    implementation('org.springframework.cloud:spring-cloud-openfeign-core:2.1.3.RELEASE') {
        force = true
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: versions.springBootVersion
    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.4.RELEASE'
    compile group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonDatabind
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: versions.springBootVersion

    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: versions.springCloudStarterNetflixHystrix
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix-dashboard', version: versions.springCloudStarterNetflixHystrix
    compile(group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springCloudStarterNetflixHystrix) {
        force = true
    }
    compile group: 'io.github.openfeign', name: 'feign-httpclient', version: versions.feignHttpClient

    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    compile group: 'uk.gov.hmcts.reform', name: 'logging-spring', version: versions.hmctsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.hmctsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-httpcomponents', version: versions.hmctsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.hmctsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: '0.1.0'
    compile group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: versions.authCheckerLib
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: versions.serviceAuthProviderClient
    compile group: 'com.github.hmcts', name: 'probate-commons', version: versions.probateCommonsVersion
    compile group: 'commons-io', name: 'commons-io', version: versions.commonsIO

    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    compile group: 'org.mapstruct', name: 'mapstruct-jdk8', version: versions.mapStruct
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: versions.jacksonDatabind

    compile group: 'org.pitest', name: 'pitest', version: versions.piTest
    compile group: 'com.atlassian.commonmark', name: 'commonmark', version: versions.commonMark
    compile 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.5.2'
    compile 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'
    compileOnly group: 'org.mapstruct', name: 'mapstruct-processor', version: versions.mapStruct
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonDatabind
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: versions.mapStruct

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'org.springframework.security', name: 'spring-security-test', version: versions.springSecurityVersion
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: versions.springBootVersion
    testCompile group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: versions.springCloudWiremock
    testCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.mapstruct', name: 'mapstruct-jdk8', version: versions.mapStruct
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonDatabind
    testCompile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: versions.jacksonDatabind
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: versions.mapStruct

    testCompile "org.junit.jupiter:junit-jupiter-api:5.6.2"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.6.2"
    testImplementation'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation('io.github.openfeign:feign-core:11.0')

    testSmokeCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    testSmokeCompile sourceSets.main.runtimeClasspath
    testSmokeCompile sourceSets.test.runtimeClasspath

    testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenityreporter
    testFunctionalCompile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
    testFunctionalCompile group: 'io.rest-assured', name: 'rest-assured', version: '4.4.0'
    testFunctionalCompile group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testFunctionalCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonDatabind
    testFunctionalCompile sourceSets.main.runtimeClasspath
    testFunctionalCompile sourceSets.test.runtimeClasspath
    testFunctionalAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    testContractImplementation(group: 'io.github.openfeign', name: 'feign-core', version: '9.7.0') {
        force = true
    }
    testContractCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'au.com.dius', name: 'pact-jvm-provider-spring_2.12', version: versions.pact_version
    testContractCompile group: 'au.com.dius', name: 'pact-jvm-provider-spring_2.12', version: versions.pact_version
    testContractCompile group: 'au.com.dius', name: 'pact-jvm-consumer-junit5_2.12', version: versions.pact_version
    testContractCompile group: 'au.com.dius', name: 'pact-jvm-consumer-java8_2.12', version: versions.pact_version
    testContractCompile("org.junit.jupiter:junit-jupiter-api:5.6.2")
    testContractRuntime("org.junit.jupiter:junit-jupiter-engine:5.6.2")
    testContractImplementation('org.junit.jupiter:junit-jupiter-api:5.6.2')
    testContractCompile sourceSets.main.runtimeClasspath
    testContractCompile sourceSets.test.runtimeClasspath
    testContractCompile(group: 'io.github.openfeign', name: 'feign-core', version: '9.7.0') {
        force = true
    }
    testContractRuntime(group: 'io.github.openfeign', name: 'feign-core', version: '9.7.0') {
        force = true
    }


}
task printbranch {
    doLast {
        print project.pactBrokerTag
    }
}

pact {
    publish {
        pactDirectory = 'target/pacts'
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
        tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
        version = project.pactVersion
    }
}

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-parameters"
}

bootJar {
    archiveName = 'probate-orchestrator-service.jar'
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

run {
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
    }
}
